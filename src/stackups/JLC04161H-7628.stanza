#use-added-syntax(jitx)
doc:\<DOC>
  Create the JLC04161H-7628 stakup.
    - 4-layer impedance control stackup
    - 1.6mm thickness
    - 1oz outer copper weight
    - 0.5oz inner copper weight

  Note: All metrics are in mm.
  Reference: https://jlcpcb.com/impedance
<DOC>
defpackage jlc-pcb/JLC04161H-7628:
  import core
  import jitx
  import jsl/layerstack
  import jlc-pcb/stackups/shared

protected val stack = make-layer-stack("JLC04161H-7628", outers) where :
  val core = FR4("core", 1.065, "core")
  val prepreg = FR4("7628", 0.2104, "7628*1")
  val outers = 
    [ 
      [copper-35um prepreg]
      [copper-15_2um core]
    ]

public val stackup = create-pcb-stackup(stack)

doc:\<DOC>
  The parameters for the `pcb-routing-structure` and `pcb-differential-routing-structure` 
  are retrieved from the PCB Impedance Calculator
   - For Coplanar Single-Ended, the valid values for Impedance is from 20 to 90.
   - For Coplanar Differential Pair, the valid values for Impedance is from 50 to 150
   - `trace-width` comes from the "Trace Width" column of the Impedance Calculator
   - `pair-spacing` comes from the "Trace Spacing" column of the Impedance Calculator
   - `clearance` comes from the "Impedance trace to copper" column of the Impedance Calculator
   - The function `velocity` calculate the velocity from the dielectric constant of the material for "7628"

  Example Usage: 
    To use the default clearance: jlc-pcb/JLC04161H-7628/SE-50()
    To use the user-given clearance: jlc-pcb/JLC04161H-7628/SE-50(clearance = 0.5)

   Reference: JLC-PCB Impedance Calculator https://jlcpcb.com/pcb-impedance-calculator
<DOC>

; For Coplanar Single-Ended
;  - width-val comes from the "Trace Width" column 
doc:\<DOC>
Create `pcb-routing-structure` for Coplanar Single-Ended with
  parameters calculated from the JLC-PCB Impedance Calculator.
   - width-val comes from the "Trace Width" column 
   - space-val comes from the "Trace Spacing" column

  The following `pcb-routing-structure`'s are created from the `#for` macro:
    - `pcb-differential-routing-structure SE-50` for Single-ended Impedance 50 ohm
    - `pcb-differential-routing-structure SE-75` for Single-ended Impedance 75 ohm

  Single-ended impedance must be in the range of 20~90 ohm

   Reference: JLC-PCB Impedance Calculator https://jlcpcb.com/pcb-impedance-calculator
<DOC>
#for ( 
  se-name in [SE-50, SE-75]
  width-val in [0.3424, 0.1311]
  ):
  public pcb-routing-structure se-name (
      -- clearance:Double = 0.508
         insertion-loss:Double = 0.004) :
    layer(Top):
      trace-width = width-val 
      clearance = clearance
      velocity = velocity("7628")
      insertion-loss = insertion-loss
    layer(Bottom):
      trace-width = width-val
      clearance = clearance
      velocity = velocity("7628")
      insertion-loss = insertion-loss

doc:\<DOC>
Create `pcb-differential-routing-structure` for Coplanar Differential Pair with
  parameters calculated from the JLC-PCB Impedance Calculator.
   - width-val comes from the "Trace Width" column 
   - space-val comes from the "Trace Spacing" column

  The following `pcb-differential-routing-structure`'s are created from the `#for` macro:
    - `pcb-differential-routing-structure DP-50` for Diffenrential Impedance 50 ohm
    - `pcb-differential-routing-structure DP-75` for Diffenrential Impedance 75 ohm
    - `pcb-differential-routing-structure DP-100` for Diffenrential Impedance 100 ohm

  Differential impedance must be in the range of 50~150 ohm.

   Reference: JLC-PCB Impedance Calculator https://jlcpcb.com/pcb-impedance-calculator
<DOC>
#for (
  dp-name in [DP-50, DP-75, DP-100],
  width-val in [0.8367, 0.4018, 0.2037],
  space-val in [0.2032, 0.2032, 0.2032]
  ):
  public pcb-differential-routing-structure dp-name (
      -- clearance:Double = 0.2032
         insertion-loss:Double = 0.004) :
    layer(Top):
      trace-width = width-val
      pair-spacing = space-val
      clearance = clearance
      velocity = velocity("7628")
      insertion-loss = insertion-loss

    layer(Bottom):
      trace-width = width-val
      pair-spacing = space-val
      clearance = clearance
      velocity = velocity("7628")
      insertion-loss = insertion-loss

