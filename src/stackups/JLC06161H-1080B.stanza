#use-added-syntax(jitx)
doc:\<DOC>
  Create the JLC06161H-1080B stakup.
    - 6-layer impedance control stackup
    - 1.6mm thickness
    - 1oz outer copper weight
    - 0.5oz inner copper weight

  Note: All metrics are in mm.
  Reference: https://jlcpcb.com/impedance
<DOC>
defpackage jlc-pcb/JLC06161H-1080B:
  import core
  import jitx
  import jsl/layerstack
  import jlc-pcb/stackups/shared

protected val stack = make-layer-stack("JLC06161H-1080B", outers) where :
  val core = FR4("core", 0.1, "core")
  val prepreg = FR4("1080", 0.0764, "1080*1")
  val prepregA = FR4("7628", 0.2104, "7628*1")
  val prepreg-core = FR4("core", 0.7, "core")
  val outers = 
    [ 
      [copper-35um prepreg]
      [copper-15_2um core]
      [copper-15_2um [prepregA prepreg-core]]
    ]

public val stackup = create-pcb-stackup(stack)

doc:\<DOC>
  The parameters for the pcb-routing-structure and pcb-differential-routing-structure 
  are retrieved from the PCB Impedance Calculator
   - For Coplanar Single-Ended, the valid values for Impedance is from 20 to 90.
   - For Coplanar Differential Pair, the valid values for Impedance is from 50 to 150
   - `trace-width` comes from the "Trace Width" column of the Impedance Calculator
   - `pair-spacing` comes from the "Trace Spacing" column of the Impedance Calculator
   - `clearance` comes from the "Impedance trace to copper" column of the Impedance Calculator
   - The function `velocity` calculate the velocity from the dielectric constant of the material for "7628"

   Reference: https://jlcpcb.com/pcb-impedance-calculator
<DOC>

; For Coplanar Single-Ended
#for ( 
  se-name in [SE-50, SE-75]
  width-val in [0.1186, 0.1186] ; no number for SE-75 "Trace Width"
  ):
  public pcb-routing-structure se-name :
    layer(Top):
      trace-width = width-val 
      clearance = 0.508       ; "Impedance trace to copper" at jlcpcb.com
      velocity = velocity("7628")
      insertion-loss = 0.004
    layer(Bottom):
      trace-width = width-val
      clearance = 0.508
      velocity = velocity("7628")
      insertion-loss = 0.004

; For Coplanar Differential Pair
#for (
  dp-name in [DP-50, DP-75, DP-100],
  width-val in [0.3553, 0.1806, 0.0978], ; the number "0.0978" is red
  space-val in [0.2032, 0.2032, 0.2032]
  ):
  public pcb-differential-routing-structure dp-name:
    layer(Top):
      trace-width = width-val
      pair-spacing = space-val ; "Trace Spacing" at jlcpcb.com
      clearance = 0.2032       ; "Impedance trace to copper" at jlcpcb.com
      velocity = velocity("7628")
      insertion-loss = 0.003

    layer(Bottom):
      trace-width = width-val
      pair-spacing = space-val
      clearance = 0.2032
      velocity = velocity("7628")
      insertion-loss = 0.003

